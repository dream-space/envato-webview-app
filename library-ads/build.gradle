plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'dreamspace.ads.sdk'
    compileSdk = 36

    defaultConfig {
        minSdk 23
        targetSdk 36

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        multiDexEnabled true
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'GradleDependency', 'ExpiringTargetSdkVersion'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'], exclude: [])
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.2.0'
    implementation 'com.google.android.gms:play-services-appset:16.1.0'
    implementation 'com.google.android.gms:play-services-basement:18.8.0'

    implementation 'androidx.lifecycle:lifecycle-process:2.9.4'
    implementation 'androidx.lifecycle:lifecycle-runtime:2.9.4'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.9.4'

    // ump sdk
    implementation 'com.google.android.ump:user-messaging-platform:3.2.0'

    //Ad Networks
    implementation 'com.google.android.gms:play-services-ads:24.6.0'
    implementation 'com.facebook.android:audience-network-sdk:6.20.0'
    implementation 'com.ironsource.sdk:mediationsdk:8.11.1'
    implementation 'com.ironsource.adapters:admobadapter:4.3.46'

    //GDPR
    implementation 'com.google.android.ump:user-messaging-platform:3.2.0'
    implementation 'com.google.android.ads.consent:consent-library:1.0.8'
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/dream-space/dreamspace-ads-sdk")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.findByName('release')
                groupId = "dream-space"
                artifactId = "dreamspace.ads.sdk"
                version = "4.0.6-all"
            }
        }
    }
}